label E1D3S5:
    
    "When I enter the lounge, I'm surprised to see a crowd surrounding the bar. I can't make out anything intelligible from the jumble of words and voices, but it must be something big based on the sheer level of excitement in the room."
    
    menu:
        "What's going on?":
            "I wander closer to the edge of the crowd and approach a pilot whose standing on her own."
            show studentF extra at cc with dissolve
            pf "Hey."
            "She responds without really seeing me."
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_11.ogg"
            stu1f "Hello."
            "I jerk my head towards the crowd."
            pf "What are all these people so excited about?"
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_12.ogg"
            stu1f "Akira's here."
            pf "Who?"
            "She turns towards me, and the frown on her face lessens when she notices my blond hair."
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_13.ogg"
            stu1f "Ah, you must be the new transfer student."
            pf "Um, yeah… Sorry, have we met?"
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_14.ogg"
            stu1f "No, but it explains why you don't know who Akira is."
            pf "Oh… Well, it seems like he's kind of a big deal."
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_15.ogg"
            stu1f "That's an understatement. He's the top pilot here at ACE."
            pf "Really?"
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_16.ogg"
            stu1f "Yeah, I heard that in his first year here he was already placed in his team's starting lineup."
            pf "As a first year?"
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_17.ogg"
            stu1f "Unreal, right? Now he's a third year and finally the team lead."
            "I crane my neck, trying to spot him. I think I catch a flash of white hair, but it's hard to tell with all of the people."
            pf "That's pretty impressive."
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_18.ogg"
            stu1f "That's not even the half of it. Akira is one of the nicest guys around! He says \"hello\" to everyone he meets and actually tries to learn the names of his classmates. When he compliments you, he means it. Even though these GEAR matches can get very competitive, Akira treats all his opponents with respect."
            "She sighs."
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_19.ogg"
            stu1f "He's a great friend to have."
            pf "Sounds like you know him pretty well."
            "Her face turns pink."
            voice "audio/voice/E1/D3/S5/stu1f/stu1f_20.ogg"
            stu1f "Oh, no, I've never actually met him."
            stop music fadeout 15
            hide studentF extra with dissolve
    
        "I bet it's a hot chick!":
            "A few guys are scattered around the outskirts of the crowd. I sidle up next to a pilot whose staring hard at the mass of people."
            show studentM extra at cc with dissolve
            pf "So, who's the babe over there?"
            "I jerk my thumb towards the center of the crowd."
            "The pilot turns to me and gives me a weird look. Then shrugs."
            voice "audio/voice/E1/D3/S5/stu2m/13.ogg"
            stu2m "Just Masato."
            "Oooh, \"Masato\"... She sounds hot!"
            pf "Seems like Masato's pretty popular."
            voice "audio/voice/E1/D3/S5/stu2m/14.ogg"
            stu2m "Yeah, well, if I were the top pilot at this school then I'd be popular too."
            "Hot {i}and{/i} smart? {i}Dayum!{/i}"
            pf "So how come you're not trying to get some of the action?"
            "He shrugs again."
            voice "audio/voice/E1/D3/S5/stu2m/15.ogg"
            stu2m "I just can't compete."
            pf "With what?"
            "He gives me an incredulous look."
            voice "audio/voice/E1/D3/S5/stu2m/16.ogg"
            stu2m "You know--Akira. He was on the starting lineup when he was just a first year."
            "My eyes widen, which seems to encourage him."
            voice "audio/voice/E1/D3/S5/stu2m/17.ogg"
            stu2m "Girls throw themselves at his feet, but he treats everyone like friends, and people just {i}like{/i} him."
            "A silence falls between us."
            pf "He?"
            voice "audio/voice/E1/D3/S5/stu2m/18.ogg"
            stu2m "Yeah."
            pf "You mean \"she\", right?"
            "He frowns and gives me another weird look, then points."
            voice "audio/voice/E1/D3/S5/stu2m/19.ogg"
            stu2m "No, I mean \"he\"."
            stop music fadeout 15
            hide studentM extra with dissolve
    
        "Are they giving out free food?!":
            "I force my way through the throng of people, much to the dismay of those beside me. Regardless, I ignore their protests and continue to make my through until I hear the pilot beside me mention something about an \"Akira\"."
            "That sounds suspiciously like a person and not enough like a food…"
            "I turn towards the two girls I'd just overheard."
            show studentF extra at l2
            show studentF2 extra at r2
            with dissolve
            pf "Hey."
            voice "audio/voice/E1/D3/S5/stu3f/Alexandria Bearden_stu3f_5.ogg"
            stu3f "Um, hi?"
            pf "What's an \"Akira\"?"
            voice "audio/voice/E1/D3/S5/stu3f/Alexandria Bearden_stu3f_6.ogg"
            stu3f "I think you mean \"who\" is an Akira…"
            "Oh. Well, there goes my chances for a free lunch. I could have really used an onigiri right now too."
            pf "Okay, who's Akira?"
            voice "audio/voice/E1/D3/S5/stu3f/Alexandria Bearden_stu3f_7.ogg"
            stu3f "How do you not know who he is? He's only the top pilot at ACE."
            voice "audio/voice/E1/D3/S5/stu4f/stu4f_05.ogg"
            stu4f "And so nice too."
            voice "audio/voice/E1/D3/S5/stu3f/Alexandria Bearden_stu3f_8.ogg"
            stu3f "Yeah! He listens to you when you talk and treats everyone like they're important."
            voice "audio/voice/E1/D3/S5/stu4f/stu4f_06.ogg"
            stu4f "Plus, he's a total dreamboat."
            "The first pilot blushes and her friend laughs."
            pf "Right… but the qualifiers haven't happened yet. You mean from last year's rankings?"
            voice "audio/voice/E1/D3/S5/stu4f/stu4f_07.ogg"
            stu4f "He's been a top pilot since his {i}first{/i} year here."
            pf "Since first year?"
            voice "audio/voice/E1/D3/S5/stu3f/Alexandria Bearden_stu3f_9.ogg"
            stu3f "Yep. He was on his team's starting lineup."
            pf "So, what year is he now?"
            voice "audio/voice/E1/D3/S5/stu4f/stu4f_08.ogg"
            stu4f "Third year."
            "They sigh wistfully."
            "Suddenly the first student perks up and grabs her friend."
            voice "audio/voice/E1/D3/S5/stu3f/Alexandria Bearden_stu3f_10.ogg"
            stu3f "Oh! He just looked at me! Did you see that?"
            voice "audio/voice/E1/D3/S5/stu4f/stu4f_09.ogg"
            stu4f "W-What! Why didn't he notice me?"
            stop music fadeout 15
            hide studentF extra
            hide studentF2 extra
            with dissolve
    
    $ E1D3S5_AkiraNoticedMe = 1
    show akira hap at cc with dissolve
    "As if on cue, a path forms amid the throng of people, and a male pilot with white hair and kind eyes walks through."
    show studentF extra at r3 with dissolve
    show studentF2 extra at l3 with dissolve
    show highschoolgirl extra at r2 with dissolve
    show highschoolgirl2 extra at l2 with dissolve
    show akira smi at cc with dissolve
    "He's laughing with the group of girls surrounding him."
    show akira mis at cc with Dissolve(2.5)
    "Our eyes meet for a second, his calm smile still intact." 
    show akira hap at cc with dissolve
    "That can't be right. {w}He must have recognised someone behind me and smiled at them."
    hide akira with dissolve
    stop music fadeout 3
    hide studentF
    hide studentF2
    hide highschoolgirl
    hide highschoolgirl2
    with dissolve
    "Regardless, after the departure of Akira and his group, the lounge feels quieter even though it's still full of students. {w}Pilots turn back to whatever programs they were watching or order drinks at the bar. {w}Others return to the games they were originally playing."
    
    "I stand awkwardly on my own; I don't see anyone I recognise. {w}Everyone seems to be intent on their own thing, so I decide to head home. It's getting late anyway."
    stop ambient fadeout 3
    scene black with fade
    
    jump E1D3S8