label E1D2S4:
    
    
    menu:
        "Пойти в ангар.":
            "Мой GEAR должен был уже прибыть. Если й пойду сейчас, то у меня будет достаточно времени на регистрацию." 
    
            pf "Так, а где ангар…?"
    
            "Я проверил карту. Похоже ангар находится по ту сторону школы. Я покосился на здания, которые, как мне казалось, находились в правильном направлении. Даже при самом быстром беге, я не думаю, что смог бы добежать до ангага и обратно, не пропустив несколько минут от начала урока, особенно раз процесс регистрации занимает время." 
    
            "Интеерсно, есть ли короткий путь…"
    
            "Я снова сверился с картой, но она не очень помогла. Она показывала только основные маршруты вокруг кампуса. Если и есть короткие пути, то я должен сам о них разузнать."
            show mayu thi at cc with dissolve
            "Как по сигналу, мимо меня прошла маленькая девушка с тиановыми марками. {w}Пилот! {w}Она определённо знает кратчайший путь к ангару."
    
            label E1D2S4_mayuloopback:
                menu:
                    "Позвать её." if (E1D2S4_mayushouldertap == 0):
                        $ E1D2S4_mayushouldertap = 1
                        pf "Эй!"
                        "Похоже, я привлёк внимание всех вокруг, {i}но{/i} не девушки. Полагаю, это само по себе достижение. Она невозмутимо шла дальше."
                        jump E1D2S4_mayuloopback
                        
    
    
                    "Одернуть её за плечо":
                        if  (E1D2S4_mayushouldertap == 1):
                            "Это никуда не приведёт. {w}Побежав трусцой, я быстро догнал её."
    
                        else:
                            "Я побежал трусцой, и быстро догнал её."
    
                        play sound "audio/sfx/Human/Poke Noise.ogg"
                        "Оказавшись достаточно близко, я осторожно дотронулся до её плеча."
                        show mayu sur at cc
                        "Она подпрыгнула."
                        show exclamation:
                            xoffset 720
                            yoffset 135
                            xzoom .75
                            yzoom .75
                        show mayu sur b1 at cc with dissolve
                        voice "audio/voice/E1/D2/S4/Mayu/1.ogg"
                        ma "Ааах!"
    
                        menu:
                            "Упс!":
                                $ E1D2S4_eeep = 1
                                pf "Ах!"
                                show mayu cur b1 at cc
                                "Я закрыл голову руками. {w}Когда ничего не произошло, я выглянул из-за рук и увидел девушку, удивлённо смотрящую на меня. Она внезапно перевела взгляд на землю, когда наши глаза встретились."
                                show mayu ner b1 at cc
                                show panic:
                                    xoffset 720
                                    yoffset 135
                                    xzoom .75
                                    yzoom .75
                                voice "audio/voice/E1/D2/S4/Mayu/2.ogg"
                                ma "И-Извини... Я не хотела напугать тебя..."
                                pf "О, нет, ты не напугала меня. Просто это--то, что я обычно… {w}Эм, я думал, я сам тебя напугал. Извини."
                                show mayu smi b1 at cc
                                voice "audio/voice/E1/D2/S4/Mayu/3.ogg"
                                ma "Всё в порядке."
                                show mayu ner b1 at cc with dissolve
                                "Её взгляд на секунду поднялся, чтобы встретиться с моим, но потом она снова уставилась вниз."
                                voice "audio/voice/E1/D2/S4/Mayu/4.ogg"
                                ma "Эммм..."
                                pf "О! Точно, у меня есть вопрос."
                                pf "Ты знаешь где ангар? Прости, тупой вопрос. Конечно же ты знаешь. На тебе форма пилота."
                                show mayu smi b1 at cc
                                "Она слабо улыбнулась."
                                pf "Есть какие-нибудь короткие пути к ангару? Как я могу судить, единственный путь к нему только по этой долгой дороге. Я как бы спешил и надеялся, что будет более быстрый способ добраться туда."
    
                            "И Оскар за переигрывание получает...":
                                $ E1D2S4_overreact = 1
                                pf "Воу! Полегче, дамочка."
                                show mayu ner b1 at cc
                                show panic:
                                    xoffset 720
                                    yoffset 135
                                    xzoom .75
                                    yzoom .75
                                voice "audio/voice/E1/D2/S4/Mayu/5.ogg"
                                ma "Извини..."
                                "Она неловко шаркает ного и смотрит на землю."
                                pf "Эй, не волнуйся. Я привык к крикам девушек."
                                "Я подмигнул ей."
                                show mayu cur b1 at cc
                                "Она смотрела на меня изумленными глазами и, похоже, не очень понимала."
                                voice "audio/voice/E1/D2/S4/Mayu/6.ogg"
                                ma "О..."
                                show mayu ner b1 at cc
                                pf "Во всяком случае, мне интересно, не могла бы ты показать как дойти до ангара."
                                "Она затихла."
                                show mayu smi b1 at cc
                                voice "audio/voice/E1/D2/S4/Mayu/7.ogg"
                                ma "Я попробую."
                                pf "Спасибо. Ты знаешь кратчайший путь в ангар?"
    
    
                            "Просто спросить про короткий путь к ангару.":
                                $ E1D2S4_quickshortcut = 1
                                pf "Как мне добраться до ангара?"
                                show mayu cur b1 at cc
                                voice "audio/voice/E1/D2/S4/Mayu/8.ogg"
                                ma "Ангара?"
                                "Она немколько раз моргнула прежде чем заметила тиановые полосы."
                                show bulb:
                                    xoffset 720
                                    yoffset 135
                                    xzoom .75
                                    yzoom .75
                                show mayu smi b1 at cc
                                voice "audio/voice/E1/D2/S4/Mayu/9.ogg"
                                ma "О, ты тоже пилот."
                                pf "Да. Я новенький, поэтому мне интересно, есть ли короткий путь." 
    
                        "Я показал маршрут на карте, и она понимающе кивнула."
                        show mayu smi at cc
                        voice "audio/voice/E1/D2/S4/Mayu/10.ogg"
                        ma "О-о... ты можешь срезать здесь..."
                        "Она показала зал пилотов."
                        voice "audio/voice/E1/D2/S4/Mayu/11.ogg"
                        ma "Там есть путь через нижний этаж, который соединён непосредственно с ангаром." 
    
                        if (E1D2S4_eeep == 1):
                            pf "Спасибо! Ты очень помогла!"
                            show mayu cur at cc
                            "Я взглянул на нее. Она дважды моргнула, прежде чем отвела взгляд."
                            show mayu ner b1 at cc
                            voice "audio/voice/E1/D2/S4/Mayu/12.ogg"
                            ma "Ох... Ничего такого..."
                            voice "audio/voice/E1/D2/S4/Mayu/13.ogg"
                            ma "М-Мне нужно идти, или я опоздаю."
                            pf "Точно, прости, что задержал тебя."
    
                        elif (E1D2S4_overreact == 1):
                            pf "Понял, спасибо."
                            show mayu cur at cc
                            "На секунду она взглянула на меня, прежде чем отвела взгляд."
                            show mayu ner b1 at cc
                            voice "audio/voice/E1/D2/S4/Mayu/14.ogg"
                            ma "... М-Мне нужно идти..."
                            pf "Хорошо. Еще раз спасибо, хорошего дня."
    
                        elif (E1D2S4_quickshortcut == 1):
                            pf "Спасибо."
                            show mayu cur at cc
                            "На секунду она взглянула на меня, прежде чем отвела взгляд."
                            show mayu ner b1 at cc
                            voice "audio/voice/E1/D2/S4/Mayu/15.ogg"
                            ma "Мне нужно идти..."
                            pf "Хорошо, всего хорошего."
    
                        $ E1D2S4_MayuGaveDirections = 1
                        hide mayu with dissolve
                        "Она слегка поклонилась, прежде чем пойти. {w}Это было, как ни странно, формально. Я изучил путь, который она показала на карте."
                        pf "Зал пилотов? Понял."
                        stop ambient fadeout 3
                        scene bg campus lounge day with fade
                        play ambient "audio/ambience/Pilot Lounge.ogg" fadein 1
                        "Следуя карте, я нашёл здание и осмотрел зал. Группы студентов собиррались вокруг бара, потягивали напитки и наблюдали за прошедшими соревнованиями на широкоэкранных телевизорах. Я бродил по комнате, проверяя разные развлечения и удобные кресла."
                        show guard extra at cc with dissolve
                        "В дальнем углу комнаты я заметил, как сторож читал портативную голограмму перед дверью. Она должно быть ведёт в ангар."
                        play sound [ "audio/sfx/Technology/ID Tap.ogg", "audio/sfx/Technology/ID Tap Fail.ogg" ]
                        "Когда я подошёл, охранник жестом указал мне приложить мою ID карту к кард-ридеру. Он издал звук и загорелся красным."
    
                        "Он нахмурился. С тяжелым вздохом он отложил ридер и протянул руку."
                        voice "audio/voice/E1/D2/S4/Guard/1.ogg"
                        "Охранник" "ID карту, полалуйста."
                        ##NEW SOUND NEEDED##play sound "audio/sfx/Technology/Passcode Entry.ogg"
                        "Я передал карту, и вбил данные в компьютер. Его пальцы летали по клавишам." 
                        voice "audio/voice/E1/D2/S4/Guard/2.ogg"
                        "Охранник" "Так, тут говорится, что твой GEAR все ещё подготавливается."
                        "Он выжидающе смотрел на меня."
                        pf "О ... Что это значит? Я не могу войти?"
                        voice "audio/voice/E1/D2/S4/Guard/3.ogg"
                        "Охранник" "Это значит, что ты получишь письмо, когда он будет готов, и ты сможешь его зарегистрировать. Как только мы получим вашу регистрацию, вы получите доступ к ангару." 
                        pf "Хорошо, спасибо."
                        "Он поворчал в знак признания и вернул карту."
                        hide guard extra
                        "Ну, это был провал, но, по крайней мере, теперь я знаю как сюда добраться."
                        "Я вернулся в зал пилотов и проверил время. Ещё около получаса. С таким же успехом я могу пойти в класс."
                        
                        scene black with fade
                        jump E1D2S8
    
        "Я должен позаботиться разрешении на парковку.":
            $ E1D2S4_GoingToGetPassNoYuuna = 1
            "Изучив карту на телефоне, я уверенно направился к зданию администрации. К счастью, в кампусе довольно легко ориентироваться, потому что все здания выглядят одинаково."
            scene bg campus building day with fade
            stop ambient fadeout 3
            "В итоге я остановился у здания администрации. Я открыл дверь и меня встретил ряд коридоров. Конечно, ещё больше поисков. Насколько они бюрократичны?"
            scene black with fade
            "После нескольких минут поворотов не туда и кучи возвратов назад, я прихожу к двери с надписью \"Администрация кампуса\"."
            scene bg campus office day with fade
            play ambient "audio/ambience/Campus Office.ogg" fadein 1
            "То, что должно быть просторным офисом в итоге заполнено раздражёнными учениками. Только один человек стоял за стойкой, и он не выглядел сговорчивым."
            
            jump E1D2S5
