label E1D2S7:
    
    stop ambient fadeout  3
    scene bg campus parking day with Dissolve(2)
    play ambient "audio/ambience/Parking Lot.ogg" fadein 1
    
    "После довольно короткой поездки я приехал в академию и стал искать место для парковки."
    play music "audio/music/Bright New Day (GAME VERSION).ogg" fadein 1
    "Каждое место на этой огромной парковке было занято каким-нибудь транспортом. Видимо, все остальные сегодня тоже приехали.  Серьёзно. Похоже, у каждого студента есть свой транспорт."
    
    "Я был почти готов сдаться, когда увидел... {w}Свободное место."
    
    "Волна головокружительных эмоций чуть не вырвалась из меня. Я практически мог видеть священный луч света, сияющий на нем, сопровождающийся пнгельским хором."
    
    "Я подъехал ближе, и потом замер. В передней части этого места на земле был знак: \"Парковка только для владельцев пропусков\"."
    
    "Ну конечно. Как же мне везёт."
    
    "Тяжело вздохнув, я начал отгонять мотоцикл назад. Полагаю, лучше..."
    
    "Подожди-ка."
    
    "Я снова оглядел парковку. Она полностью лишена жизни, что означает…"
    
    menu:
        "Нет свидетелей.":
            "Если никто не видел, что я занял это место, значит, я не сделал ничего плохого?"
    
            "Отбрасывая все сомнения, я поставил байк на свободное место. Видишь? Ничего плохого не случилось."
    
            "Теперь, пора идти в класс--"
            
            stop music fadeout 3
            play sound "audio/sfx/Vehicles/Bike Revving.ogg" fadein 3 fadeout 3
            "Я обернулся как раз вовремя. Ко мне подъехал другой мотоцикл. Судя по растрепанной форме, парень тоже студент, но яростный хмурый взгляд убеждал меня, что он не собирался заводить друзей." 
            
            play music "audio/music/Stress (GAME VERSION).ogg" fadein 3
            show bully3 extra at cc with dissolve
            show exclamation:
                xoffset 675
                yoffset 50
                xzoom .75
                yzoom .75
            voice "audio/voice/E1/D2/S7/Ken/1.ogg"
            kt "Эй! Какого хрена ты делаешь?!"
            voice "audio/voice/E1/D2/S7/Ken/2.ogg"
            kt "Убери эту херню с моего места. Сейчас же!"
    
            "Оскалив зубы, он указал пальцем за собой. Отлично. Отлично. Из всех людей, которые могли бы владеть этим местом, это должен был быть кто-то вроде {i}него{/i}."
            voice "audio/voice/E1/D2/S7/Ken/3.ogg"
            kt "Ты не слышал меня? Вали!"
    
            "Он спрыгнул с байка и пошёл ко мне."
    
            menu:
                "Лучше не встрявать в драку в первый день.":
                    pf "Хорошо, хорошо, дай секудку."
    
                    "Я неохотно убрал байк с места."
    
                    "Как бы мне не было больно отказываться от этого места, я чувствовал, что он не из тех, кто избежит драки. Драка в первый день вряд ли что-то сделает с моей репутацией."
                    "К тому же, это {i}его{/i} место."
                    voice "audio/voice/E1/D2/S7/Ken/4.ogg"
                    kt "... Да, так я и думал."
                    hide bully3 extra with dissolve
                    "Он издевательские усмехнулся прежде чем сел на свой байк"
                    "Полагаю, лучше найти другое место."
                    jump E1D2S7_FarParking
    
                "\"Херню\"? Кто бы говорил!":
                    pf "ПожалуйстаЮ будь так добр расскажи, что ты там назвал кучей болтов?"
    
                    "Его байк едва подходил для дорог. Это древняя, ржавая, смертельная ловушка, которая выглядела так, как будто она передавалась из поколения в поколение, пока наконец, не стала его."
                    voice "audio/voice/E1/D2/S7/Ken/5.ogg"
                    kt "Что ты только что…?"
                    show vein:
                        xoffset 675
                        yoffset 50
                        xzoom .75
                        yzoom .75
                    "Его лицо покраснело. Похоже, я задел за живое."
    
                    pf "Ты слышал. Похоже, что этот металлолом едва удерживается скотчем."
    
                    "Практически дрожа от гнева, он встал так, чтобы его байк не было видно, возможно, он боялся, что я {i}действителньо{/i} замечу на нём скотч."
    
                    "Он похрустел пальцами в жалкой попытке запугать меня."
                    voice "audio/voice/E1/D2/S7/Ken/6.ogg"
                    kt "Считаешь себя крутым? Ну, когда я с тобой закончу, ты так не будешь выглядеть."
    
                    "Он жестом вызвал меня выйти вперед, затем поднял кулаки. Я сжал свои кулаки в ответ."
                    voice "audio/voice/E1/D2/S7/Ken/7.ogg"
                    kt "Или ты слишком напуган?"
    
                    "Я засмеялся. Напуган? Червяком, вроде него? Плавным движением, Я спрыгнул с байка и медленно пошёл к нему."
                    $ E1D2S7_BullyFight = 1
    
                "Не повезло, приятель.":
                    "Я пожал плечами прежде чем слез с мотоцикла, но он остановил меня, когда я пытался пройти мимо него."
                    show question:
                        xoffset 675
                        yoffset 50
                        xzoom .75
                        yzoom .75
                    voice "audio/voice/E1/D2/S7/Ken/8.ogg"
                    kt "Думаешь, можешь вот так игнорировать меня?"
    
                    pf "Я пытаюсь, но ты мешаешь."
                    show vein:
                        xoffset 675
                        yoffset 50
                        xzoom .75
                        yzoom .75
                    "Судя по его углубляющейся злобе и прищуренным глазам, я могу сказать, что он не будет удовлетворен, пока один из нас не будет лежать в крови на земле. Отлично, сначала эта сумасшедшая девушка, а теперь этот придурок. Сегодня просто не мой день."
                    $ E1D2S7_BullyFight = 1
    
            "Напряжение между нами ощутимо. Я сжал кулаки, разжигая огонь в его глазах."
    
        "Это место вмё ещё не моё.":
            $ E1D2S7_CleanMove = 1
            "Это все еще не мое место. Я найду место за пределами парковки. {w}Следуя указателям, я проехал мимо рядов зарезервированных мест."
            jump E1D2S7_FarParking
            
    if (E1D2S7_BullyFight == 1):
    
        "Он переходит в боевую стойку; его руки подняты, чтобы защитить лицо. Он определенно не первый раз дрался. Лучше не стоит его недооценивать. Я соответствую его позиции, а затем--"
    
        $ qtebase = 3
        $ qtetotal = qteath + qtebase
        $ t_var = qtetotal
        show screen timer_scr(place="E1D2S7_FightTImeout")
        
        menu:
        
            "{color=#00ff00}{b}First Blood!{/b}{/color}" if (MCStory == 1):
                jump E1D2S7_FirstBlood
        
            "Первая кровь!" if (MCStory != 1):
                label E1D2S7_FirstBlood:
                $ renpy.hide_screen ("timer_scr")
                "Я подался вперёд и сделал нихкий замах ногой."
                
                "Когда его рука начала переходить в блок, я застал его врасплох ударом с разворота в голову."
                play sound "audio/sfx/Human/Med_Punch.ogg"
                "Он пролетел несколько метров, прежде чем упал на асфальт."
                hide bully3 extra with dissolve
                "Не спеша, я расслабился и вздохнул."
                "Я подошёл к его телу и легко тронул ногой."
                play sound "audio/sfx/Human/Poke Noise.ogg"
                "Он не шевелился."
                "Возможно, я переоценил его. Хреново, этот парень едва стоил усилий."
    
                "Я не {i}так{/i} сильно его ударил, так что к уроку оклимается… Я думал."
                "Выдохнув, я спокойно прошёл мимо его бессознательного тела."
                $ E1D2S7_BullyFightWin = 1
                jump E1D2S8
    
            "Сделаем это красиво...":
                $ renpy.hide_screen ("timer_scr")
                "Хех. Хорошо, что я взял эти пять уроков каратэ, когда мне было десять лет. У этого неудачника не будет и шанса, когда он увидит, что я могу сделать!"
    
                "Увидев момент, я приготовился к пинку топором. Высоко подняв ногу, я стремился к мягкой плоти между его шеей и ключицей, но когда моя нога поднялась примерно до уровня середины живота, она застряла."
                play sound "audio/sfx/Human/light_punch.ogg"
                "Моя промежность выдала болезненное предупреждение, и я , потеряв равновесие, упал на спину."
    
                "Я сделал ужасную ошибку. Я забыл, что я не такой гибкий, как в десять лет."
                "Парень согнулся от смеха, и я стиснул зубы, желая преодолеть боль."
                "Внезапно наменя упала тень. Я узнал форму охранника."
                show guard extra at r3 with dissolve:
                    xzoom -1
                voice "audio/voice/E1/D2/S7/Guard/1.ogg"
                gua "Что ты делаешь на земле?"
                pf "Ничего. Я просто упал."
                "Охранник выглядел недоверчивым, и глянул на другого парня, который кивал в подтверждении."
                voice "audio/voice/E1/D2/S7/Guard/2.ogg"
                gua "Хорошо, тогда, чёрт возьми, поднимайся. Что вы двое делаете на парковке?"
                "Морщась, мне удалось встать на ноги."
                pf "Я просто хотел припарковать мотоцикл."
                voice "audio/voice/E1/D2/S7/Ken/9.ogg"
                kt "Да, но на {i}моём{/i} месте!"
                voice "audio/voice/E1/D2/S7/Guard/3.ogg"
                gua "Мы можем легко разрешить это. Покажите мне ваши пропуска."
                "Этот придурок немедленно вытащил свой. Я мог только застенчиво помотать головой."
                pf "У меня нет его."
                voice "audio/voice/E1/D2/S7/Guard/4.ogg"
                gua "Тогда у тебя нет права парковаться здесь. Иди перепаркуй свой мотоцикл на гостевую парковку."
                hide bully3 extra
                hide guard extra
                with dissolve
                "Неохотно, я убрал свой байк."
                jump E1D2S7_FarParking
    
            "Погоди...":
                $ renpy.hide_screen ("timer_scr")
                "Я стоял в стойке, ожидая, что он сделает первый ход."
    
                "Вместо этого он {i}танцевал{/i} на месте, искушая меня сделать шаг к нему. Я не проглочу наживку; Я хочу, чтобы он ударил первым."
                "Он сделал пару ложных атак, пока я осторожно обходил его."
                voice "audio/voice/E1/D2/S7/Ken/10.ogg"
                kt "Чего ты ждёшь? Давай!"
    
                "Я сделал ложный пинок, и улыбнулся, когда он вздрогнул. Он не все ещё не атаковал. Просто следил за мной." 
                voice "audio/voice/E1/D2/S7/Guard/5.ogg"
                gua "Кто-нибудь из вас сделает первый шаг? Вы тратите моё время."
                show guard extra at r3 with dissolve:
                    xzoom -1
                "Мы оба замерли и посмотрели на крепкого охранника рядом с нами. Его руки скрещены на груди, а униформа напрягала мышцы."
                voice "audio/voice/E1/D2/S7/Guard/6.ogg"
                gua "Ну?"
                "Другой парень попвтался обойти охранника, но он остановил его.." 
                voice "audio/voice/E1/D2/S7/Guard/7.ogg"
                gua "Покажите мне свои пропуска на парковку. Или мы можем разобраться с этим в моем офисе. Ваш выбор."
                "Охранник пристально смотрел на нас. Я качал головой, когда придурок рядом со мной вытаскивал пропуск."
                voice "audio/voice/E1/D2/S7/Ken/11.ogg"
                kt "Мой прямо тут."
                "Охранник осмотрел его, а затем вернул парню. Он смотрел на меня и указал через плечо."
                voice "audio/voice/E1/D2/S7/Guard/8.ogg"
                gua "Убирайся отсюда и не заставляй меня делать свою работу. Я должен быть сейчас на перерыве."
                hide guard extra with dissolve
                "Он повернулся и ушёл, ворча о тупых учениках. Я пошёл к мотоциклу, и заметил самодовольный взгляд этого придурка."
                hide bully3 extra with dissolve
                "Я ничего не сказад пока выезжал на байке для поиска другого места для парковки." 
                jump E1D2S7_FarParking
    
            "Будем аккуратны!":
                $ renpy.hide_screen ("timer_scr")
                "Лучшее нападение - это хорошая защита. Я {i}хотел{/i}, чтобы он атаковал первым. Так я оценю его силы и он быстрее вымотается."
    
                "Я встал в защитную стойку и ждал. Как и ожидалось, он двинулся первым."
    
                "Он подавлся вперёд и замахнулся ногой по лицу."
                play sound "audio/sfx/Human/light_punch.ogg"
                "Я отразил атаку. {w}Хотя его движения сильны, ему не хватает скорости и ловкости, которые, к счастью для меня, являются моими сильными сторонами." 
                ##NEW SOUND NEEDED## - (put back in on 05/01/16, but should check if this is alright)
                play sound [ "audio/sfx/Human/light_punch.ogg", "audio/sfx/Human/Med_Punch.ogg", "audio/sfx/Human/light_punch.ogg", "audio/sfx/Human/Med_Punch.ogg"  ]
                "С разочарованным ворчанием он наносил новые и новые удары, каждый удар становился все более отчаянным, чем последний, поскольку он постоянно не мог поразить цель."
    
                "Вскоре его лицо стало красным, и он задыхался. Его атаки стали более беспорядочными, и половина просто проходила мимо. Между тем, я едва вспотел."
                show vein:
                    xoffset 675
                    yoffset 50
                    xzoom .75
                    yzoom .75
                voice "audio/voice/E1/D2/S7/Ken/12.ogg"
                kt "Сразись со мной уже!"
    
                "Я просто ухмыльнулся ему. Он взревел от ярости и неустойчиво нанёс еще один удар, от которого я легко уклонился. Интересно, как долго я смогу продолжать в том же духе, прежде чем он рухнет от истощения."
    
                "Тем не менее, это продолжалось достаточно долго, и я мог бы быть сейчас в месте получше. Думаю, пора менять ситуацию."
                ##NEW SOUND NEEDED## - (put back in on 05/01/16, but should check if this is alright)
                play sound "audio/sfx/Human/Med_Punch.ogg"
                "Когда он снова атаковал, я заблокировал её рукой."
                play sound "audio/sfx/Human/Med_Punch.ogg"
                "После этого я подался вперёд и ударил его в солнечное сплетение. {w}Он начал задыхаться, когда из него выбили воздух, но я не остановился."
                play sound "audio/sfx/Human/light_punch.ogg"
                "Подняв колено к груди, я словил его ударом сбоку, и он упал не землю."
                hide bully3 extra with dissolve
                "Я ждал, пока он встанет, и когда он этог оне сделал, я проверил свой байк, чтобы убедиться, что он в порядке. После, я ушёл."
    
                "Он будет в порядке… А если нет, то кто-нибудь найдёт его… в итоге."
                $ E1D2S7_BullyFightWin = 1
                jump E1D2S8
    
            "Попытаемся...": 
                $ renpy.hide_screen ("timer_scr")
                "Я замахнулся кулаком по лицу. Он отшёл в последний момент, но я не мог остановиться."
                play sound "audio/sfx/Human/light_punch.ogg"
                "Я пытался балансировать, но ноги, похоже, не слушались, и я упал." 
    
                "Я посмотрел на парня, который заливался смехом. Поднявшись на ноги, я приготовился ударить его снова, когда крепкая рука схватила меня за плечо." 
                show guard extra at r3 with dissolve:
                    xzoom -1
                voice "audio/voice/E1/D2/S7/Guard/9.ogg"
                gua "Хорошо, пора остановиться. На это слишком больно смотреть." 
                "Другой парень старается сохранять невозмутимый вид, но время от времени он не мог сдержать смех."
                voice "audio/voice/E1/D2/S7/Guard/10.ogg"
                gua "Над чем смеёшься?"
                "Он захлопнулся."
                voice "audio/voice/E1/D2/S7/Guard/11.ogg"
                gua "Вы, оба, покажите мне парковочный пропуск."
                "Придурок вытащил свой пропуск и передал охраннику. Когда охранник посмотрел на меня, я мог только пожать плечами." 
                pf "У меня нет пропуска."
                voice "audio/voice/E1/D2/S7/Guard/12.ogg"
                gua "Так ты пытался бороться за что-то, что даже не твое? Убирайся отсюда." 
                "Он указал пальцем через плечо на то, что, как я предполагал, находится за пределами парковки кампуса." 
                hide bully3 extra
                hide guard extra
                with dissolve
                "Я сел на байк и выехал, желая, чтобы у меня был пропуск. Тогда я бы мог выставить этого парня засранцем. Пока я выезжал, то слышал как охранник отчитывал парня, что делало меня немного счастливым… но только немного." 
                jump E1D2S7_FarParking
    
    label E1D2S7_FightTImeout:
        $ renpy.hide_screen ("timer_scr")
        "Я замер, и в этот момент сомнения кулак столкнулся с моим животом. Я съежился, ожидая неизбежного второго удара, но ничего не произошло."
        show guard extra at r3 with dissolve:
            xzoom -1
        "Крепкий охранник сдерживал парня."
        voice "audio/voice/E1/D2/S7/Guard/13.ogg"
        gua "Это парковка, а не боксёрский ринг."
        show storm:
            xoffset 675
            yoffset 50
            xzoom .75
            yzoom .75
        "Парень продолжал напрасно бороться. Когда он понял, что ничего не выйдет, он успокоился, и охранник отпустил его."
        voice "audio/voice/E1/D2/S7/Guard/14.ogg"
        gua "Кто-нибудь из вас вообще должен быть тут? Где ваши пропуска?"
        voice "audio/voice/E1/D2/S7/Ken/13.ogg"
        kt "Вот."
        "Охранние посмотрел на его пропуск и кивнул, потом посмотрел на меня."
        voice "audio/voice/E1/D2/S7/Guard/15.ogg"
        gua "Ну?"
        pf "У меня нет пропуска…"
        voice "audio/voice/E1/D2/S7/Guard/16.ogg"
        gua "Тогда проваливай."
        hide bully3 extra
        hide guard extra
        with dissolve
        "С болью в животе я начал уезжать. Я мог слышать позади крик охранника."
        voice "audio/voice/E1/D2/S7/Guard/17.ogg"
        gua "Над чем ты ухмыляешь? Паркуйся и вали отсюда!"
        "Внезапно, я не чувствовал себя так плохо."
        jump E1D2S7_FarParking
        
    label E1D2S7_FarParking:
        
        $ E1D2S7_ParkedFar = 1
        stop music fadeout 3
        scene black with fade
        "Кажется, прошла вечность, но я наконец-то нашёл пустое, без всяких знаков, место. Я припарковался, спрыгнул с байка и оглянулся назад, как я добрался."
        stop ambient fadeout 3
        scene bg campus main day with fade
        play ambient "audio/ambience/Campus.ogg" fadein 1 fadeout 1
        "Сердце кровью обливалось, когда я видел насколько маленькими выглядели эти огромные здания кампуса. Это не будет весёлой прогулкой назад."
        "Не имея другого выбора, я пошёл обратно в кампус и вернулся как раз вовремя, чтобы найти свой класс."
        
        
        jump E1D2S8
