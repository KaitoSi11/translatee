label E1D2S9:
    
    play ambient "audio/ambience/Hangar.ogg" fadein 1
    scene bg campus hangar day with fade
    
    "Я шёл по длинному коридору, который вёл к огромное место, заполненое возвышающимеся GEARs. {w}Каждый из них изящен, изыскан, и обновлён последними аксессуарами и оружием. {w}Они не шутили, когда говорили, что Япония является лидером меха технологий."
    
    "Я шёл мимо несколько рядом GEAR, пока..."
    play music "audio/music/Evening Out (GAME VERSION).ogg" fadein 1
    #scene cg GEAR hangar first at Zoom((1920, 1080), (0, 0, 3840, 2160), (0, 0, 3840, 2160), 0) with Dissolve(2.5)
    
    #show cg GEAR hangar first with Dissolve(2.0):
    #    xzoom .9
    #    yzoom .9
    #    yoffset -800
    #    linear 8.0 yoffset 0
        
    #$renpy.pause(4.0, hard=True)
    #"...Не воссоединился с Орлом."
    
    #show cg GEAR hangar first with Dissolve(1.0):
    #    parallel:
    #        linear 4.0 xzoom .5
    #    parallel:
    #        linear 4.0 yzoom .5
    #    parallel:
    #        linear 2.0 yoffset 0

    $ persistent.gpix[3][0] = 1
    show cg GEAR hangar first with Dissolve(2.0):
        xzoom 1.5
        yzoom 1.5
        yoffset -500
        linear 5.0 yoffset 0
        
    $renpy.pause(3.0, hard=True)
    "...Я воссоединился с Орлом."
    
    show cg GEAR hangar first with Dissolve(1.0):
        parallel:
            linear 4.0 xzoom 1
        parallel:
            linear 4.0 yzoom 1
        parallel:
            linear 2.0 yoffset 0
            
        
    $renpy.pause(4.0)
    "Толпа студентов окружала его. Они тихо говорили, но начали хихикать, когда заметили меня."
    
    "Я приблизился  кним, но они разошлись прежде чем я добрался до них. Большинство пытались скрыть свой смех. Неудачно."
    
    "Я детально осмотрел свой GEAR, чтобы убедиться, что он не пострадал во время перевозки. {w}Всё нормально. Мой GEAR может немного более громоздкий, чем другие, но он в отличном рабочем состоянии, что, в итоге, имеет значение. Цвета все еще яркие. Интересно, его отполировали, или он всегда так хорошо выглядел?."
    $ ss = Character("Shou Shinjirou", color="#01DF3A")
    stop music fadeout 5
    voice "audio/voice/E1/D2/S9/Shou/23.ogg"
    ss "Другие парни могут смеяться над твоим GEAR, но я чувствую, что это другое."
    scene bg campus hangar day with fade
    show shou mis at cc with dissolve
    voice "audio/voice/E1/D2/S9/Shou/24.ogg"
    ss "Это дух."
    show note:
        xoffset 720
        yoffset 20
        xzoom .75
        yzoom .75
    show shou hap at cc
    voice "audio/voice/E1/D2/S9/Shou/25.ogg"
    ss "Что-то, что выделяет его среди других. Да, я чувствую, что он и правда особенный."
    play music "audio/music/Baka! (GAME VERSION).ogg" fadein 1
    "Что? Я не заметил студента, стоящего позади меня. Его руки скрещены, и он кивал с закрытыми глазами, как будто он говорил что-то действительно глубокое и значимое."
    
    menu:
        "Чем-то могу помочь…?":
            "Он кажется достаточно безобидным… {w}Вроде бы. Можно глянуть, что он хочет. Возможно, я бы и сам задал несколько вопросов."
    
            pf "Эй, чем-то помочь тебе?"
    
        "Чёрт побери, он особенный!":
            "Я рад, что хоть {i}кто-то{/i} знает толк в  GEAR, когда видит их. Я тоже скрестил руки и кивнул. У меня возник соблазн попросить его повторить это, чтобы я мог записать эту мудрость."
    
            pf "Да, черт побери. Я думаю, что другие просто напуганы."
    
        "Отойти от этого безумца.":
            "Определённо что-то \"не так\" с этим парнем. Кто говорит такие вещи? Особенно тому, кого ты только что встретил."
    
            show shou hap at cc with dissolve:
                xzoom 0.75
                yzoom 0.75
                
            "Я отстранился от него, надеясь, что он поймёт намёк."
            #shou sprite movements maybe
            "..."
    
            show shou hap at cc with dissolve:
                xzoom 1
                yzoom 1
                
            "Он синхронно приближался, когда я отходил. Когда я случайно посмотрел на него, он убылнулся. Хорошо… теперь я немного напуган."
    
            "Может отвлечь его разговором, пока не найду способ сбежать."
    
    pf "Эм, тебе что-то нужно?"
    show shou sur at cc with dissolve
    show shocked:
        xoffset 720
        yoffset 20
        xzoom .75
        yzoom .75
    voice "audio/voice/E1/D2/S9/Shou/2.ogg"
    ss "Oh, right!"
    stop music fadeout 3
    "Он выглядел почти удивленным, как будто забыл, зачем пришел. Он так суетился, что было интересно, нервничал ли он."
    show shou smi at cc
    play music "audio/music/Bright New Day (GAME VERSION).ogg"
    voice "audio/voice/E1/D2/S9/Shou/3.ogg"
    ss "Сё. Сё Синдзиро. Рад познакомиться!"
    show shou hap at cc
    "Он, с улыбкой до ушей, проятнул мне руку. Я осторожно пожал её."
    
    pf "Эм, привет, я [pfull]."
    show shou smi at cc
    voice "audio/voice/E1/D2/S9/Shou/4.ogg"
    ss "ты же пилот на втором году, да?"
    
    pf "Да. Как ты--"
    show shou mis at cc
    voice "audio/voice/E1/D2/S9/Shou/5.ogg"
    ss "Знаешь ли, не так много студентов переводятся на втором году обучения. О тебе говорили в зале пилотов! Таинственный, иностранный студент."
    
    "Это должно объяснить интерес к моему GEAR. Думаю, я не вполне оправдал их ожидания. Хотя, я не уверен чего они ожидали."
    show shou hap at cc
    show bulb:
        xoffset 720
        yoffset 20
        xzoom .75
        yzoom .75
    voice "audio/voice/E1/D2/S9/Shou/6.ogg"
    ss "И я понял, что {i}ты{/i} и есть переведённый студент."
    
    "Интересно, что меня выдало? Не может быть, чтобы это были мои великолепные золотые волосы."
    show shou ske at cc with dissolve
    voice "audio/voice/E1/D2/S9/Shou/7.ogg"
    ss "Должен сказать… Я думал, ты повыше будешь."
    show shou neu at cc
    "Он начал сравнивать наш рост рукой и немного разочаровался тем, что мы примерно одинаковой роста. И снова, чего он ожидал? Мы все не выглядим как {i}Сеонардо ДиЛаприо{/i}. Я думаю, что его восприятие Америки немного искажено…"
    show shou smi at cc
    voice "audio/voice/E1/D2/S9/Shou/8.ogg"
    ss "В любом случае, ты новенький, а это значит, что ещё не состоишь в команде, да?"
    
    "Точно. Ещё с конца занятий это было на уме. Я кивнул, почти уверенный, что знал, что он собирался сказать дальше."
    show shou hap at cc
    voice "audio/voice/E1/D2/S9/Shou/9.ogg"
    ss "Ну, как раз получилось, что нам не жизненно не хватает четвёртого члена в команду, чтобы мы смогли состязаться."
    
    "Он посмотрел на меня с надеждой, Потом наклонился ближе… слишком близко. Личное пространство, чувак!"
    show shou mis at cc with dissolve
    voice "audio/voice/E1/D2/S9/Shou/10.ogg"
    ss "You see where I'm going with this, don't you? В целом, ты должен присоединиться к моей команде, потому что мы потрясающие."
    $ E1D2S9_AgreeJoinShouTeam = 0
    menu:
        "Звучит забавно. Подписываюсь!":
            "Какого чёрта, он выглядит достаточно дружелюбно. К тому же, я не совсем в том положении, чтобы выбирать команду."
    
            pf "Да, почему бы и нет?"
            show shou hap at cc
            show note:
                xoffset 720
                yoffset 20
                xzoom .75
                yzoom .75
            voice "audio/voice/E1/D2/S9/Shou/11.ogg"
            ss "Вот о чём я и говорю, братишка!"
    
            "Его лицо озарило восхищение, и должен признать, это чувство немного заразное."
            show shou smi at cc
            voice "audio/voice/E1/D2/S9/Shou/12.ogg"
            ss "Погнали, наш зал там. Представлю тебя команде!"
            hide shou with dissolve
            "Мы направились в зал."
            voice "audio/voice/E1/D2/S9/Shou/13.ogg"
            ss "Так… С чего бы начать? О, точно, есть там одна девушка--" 
            $ E1D2S9_AgreeJoinShouTeam = 1
    
        "Прости. Я хочу посмотреть все варианты.":
            pf "Как бы забавно это ни звучало, я хотел бы проверить другие команды, прежде чем вступить в какую-нибудь."
            show shou ner at cc with dissolve
            voice "audio/voice/E1/D2/S9/Shou/14.ogg"
            ss "Оууу."
    
            "На секунду он выглядел пости разбитым горем, но быстро вернулся к своей глупой усмешке."
            show shou hap at cc
            voice "audio/voice/E1/D2/S9/Shou/15.ogg"
            ss "Это здорово, я понял. Ерунда."
    
            "Я рад, что он так хорошо принял это. Я волновался, что он не оставил бы меня в покое, пока я бы не присоединился к нему."
            show shou smi at cc
            voice "audio/voice/E1/D2/S9/Shou/16.ogg"
            ss "Большинство пилотов зависают зале пилотов. Если кто-то ищет людей в команду, то ты найдёшь их там."
    
            pf "Спасибо. Увидимя."
            show shou mis at cc
            voice "audio/voice/E1/D2/S9/Shou/17.ogg"
            ss  "Конечно. Удачи, братишка. Просто помни, что мы всегда тут, если ты изменишь решение!"
            hide shou with dissolve
            "Мы попрощались прежде чем разойтись."
    
        "Хаха. Нет.":
            "Я не уверен, что хочу застрять с этим чудаком дольше, чем нужно."
            pf "Спасибо, но нет."
            show shou ner at cc
            show drop:
                xoffset 720
                yoffset 20
                xzoom .75
                yzoom .75
            voice "audio/voice/E1/D2/S9/Shou/18.ogg"
            ss "Ох…"
            pf "Я не уверен, что хорошо подойду."
            show shou sad at cc
            "Его лицо поникло, и я почти стало жалко его, но его нахмуренность быстро сменилась обычной усмешкой."
            show shou hap at cc with dissolve
            voice "audio/voice/E1/D2/S9/Shou/19.ogg"
            ss "Не беспокойся, чувак. Я понял. Вы новенький и хочешь проверить всё, прежде чем приниять решение, не так ли?"
            pf "Верно. Конечно. Без разницы."
            show shou smi at cc
            voice "audio/voice/E1/D2/S9/Shou/20.ogg"
            ss "Ладно, хорошо, пилоты обычно болтаются в зале пилотов. Можешь представиться и посмотреть, как пойдёт."
            pf "Спасибо."
            "Этот парень удивительно спокоен ко всему…"
            show shou mis at cc
            voice "audio/voice/E1/D2/S9/Shou/21.ogg"
            ss "не уверен, что много команд открыто для другого участника."
            "Ха, я попытаюсь."
            pf "Хорошо, спасибо за подсказку. Может быть, ещё увидимся."
            show shou hap at cc
            voice "audio/voice/E1/D2/S9/Shou/22.ogg"
            ss "Удачи, братишка."
            hide shou with dissolve
            "Все еще ухмыляясь, он помахал рукой, и затем ушёл."
            "Не мог сказать, обидел ли я его чувства или нет. Ну, не моя проблема. В последний раз взглянув на свой GEAR, я тоже ушёл."
    
    stop music fadeout 3
    if (E1D2S9_AgreeJoinShouTeam == 0):
        "Итак, лучше всего сейчас отправиться в зал пилота."
    
        if (E1D2S4_MayuGaveDirections == 1):
            scene bg campus hangar day with fade
            "Раз я ранее пришёл в ангар через зал пилотов, то знал куда направляться, и не беспокоиться о карте кампуса."
            "Через несколько минут я уже был у двери зала."
    
    
        else:
            "Я снова открыл карту кампуса. Красная точка загорелась, когда я пытался выбрать зал пилотов."
            "Я собирался выходить, когда увидел группу пилотов, идущих в другом направлении."
            "Может, им нужен еще один член команды?"
            "Они уже были далеко от меня, поэтому я поторопился, чтобы догнать и пойти за ними через извилистый тоннель. Я был довольно уверен, что так людей и убивали…"
            "В итоге, группа исчезла за дверью, которая вела… В зал пилотов. Будем знать!"
            
    scene black with fade
    stop ambient fadeout 3
    jump E1D2S10
