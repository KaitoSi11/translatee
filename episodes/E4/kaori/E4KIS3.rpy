#WEEKEND EVENT
label E4KIS3:

$ kaoHairF = "default"
$ kaoHairB = kaoHairF
$ kaoOut = "hotel"

play music "audio/music/Bright New Day (GAME VERSION).ogg" fadein 5
scene bg homekaito myroom day with fade


"Before my alarm sounds, I blink open my eyes and hop out of bed. Animecon is today and I can't think of a better person to go with than Kaori!" 
"I quickly get ready, then scavenge the fridge for food. I need to stock up on calories so my pockets won't be gouged by overpriced mediocre venue food."
"Nikki is working at the restaurant today and Uncle Kaito and Aunt Yuki have gone on some sort of trip together. I don't really know where, but they were in even more of a hurry to leave than I am."

scene black with fade

"As I eat, I browse my tablet and check out the different options for the con today. There are panels held by all sorts of people, cosplay contests, and lots of merchandise for purchase. I wish I could check out everything!" 
"Once I'm done eating, I clean up, then drive myself to the train station."


scene bg travel trainstation dusk with fade

"Kaori waits on the platform."

pf "Hey!"

show kaori smi at cc with dissolve

"She turns around at the sound of my voice and smiles."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_121.ogg"
ki "Hey."
pf "All ready to go?"
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_122.ogg"
ki "Of course. I mapped out the most effective way to see all the best panels and guest stars while still leaving time to browse around the marketplace."
"Is there a situation she doesn't have to strategise? Still, I did say I wanted to hit as much as possible."
pf "Sounds like a game plan."

scene black with fade

"Soon our train pulls into the station and we quiet down as we get on. The hum of the motor makes me sleepy and my eyes droop. Kaori stares out the window. I wonder what she's thinking…"
"After a lengthy ride, we arrive in the city and head to the convention center." 

scene bg campus building day with fade

"The first thing I notice is the sheer number of people. I feel like I'm back in New York! After living in Isokaze for so long, I've almost forgotten what a crowded room feels like."
"I drink in the sights and sounds. The constant buzz of laughter and chatter hangs heavy in the air and circles around the lines of booths. I try to read all the posters… especially the ones with cute girls on them…"
"Suddenly, Kaori grabs my arm and I jilt back to reality already feeling guilty. She wasn't paying attention to me though, as she points to a group of cosplayers dressed as Ninja Rangers."

show kaori sur at cc with dissolve
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_123.ogg"
ki "Look!"
pf "Go get a picture with them!"
show kaori cur
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_124.ogg"
ki "You don't think they'll mind?"
pf "I think they'd enjoy the attention."
show kaori hap
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_125.ogg"
ki "Okay!"
hide kaori with dissolve
"She seems giddy as she practically bounces over to the group. I see them all grin and nod enthusiastically, and Kaori motions for me to take a photo."  
"As I pull out my phone, the Rangers surround Kaori and each strikes their character's signature pose. It's like I'm looking at a real Ninja Rangers poster!"
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_126.ogg"
ki "Thanks, guys."
"They give her a thumbs up, seeming pleased with themselves. Kaori returns and looks at the picture."
show kaori smi at cc with dissolve
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_127.ogg"
ki "That actually came out pretty well!"
"She doesn't have to sound so surprised. I'm an expert photographer."
show kaori ner
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_128.ogg"
ki "Oh sorry! I probably should have asked if you wanted your photo taken."
"I shake my head."
pf "It's fine. I'm just here to soak in the environment."
show kaori smi
"She nods, then grabs my hand."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_129.ogg"
ki "C'mon, let's go check out the first panel on our list!"

scene black with fade

"Kaori seems to know exactly where she's going as she drags me through the crowd. Finally we stop before a big sign that says \"How To Make Visual Novels\"!"

scene bg nightclub with fade
pf "I didn't know you liked visual novels."
show kaori cur at cc with dissolve
"She shrugs."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_130.ogg"
ki "I guess it never came up."
"I bet Kaori plays them for the same reason I do."

menu:
    "She must enjoy the storyline.":
        pf "I haven't played too many, but what I enjoy about them is that they focus a lot on creating an interesting storyline and plot."
        show kaori hap
        "Kaori nods excitedly."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_131.ogg"
        ki "Yes, exactly! Most games focus so much on player controls and character manipulation that they forget all about the storyline. I like that visual novels are the opposite of that and give the player free reign to choose how they want the story to go."
        show kaori smi
        "Kaori plays video games… and likes them?! This is even more surprising than learning she likes anime!"

    "Obviously it's for the characters.":
        pf "I like that visual novels focus so much on character interactions."
        show kaori smi
        "Kaori nods."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_132.ogg"
        ki "That's true. I mainly enjoy the storyline, but a strong cast will always make the adventure more fun!"
        "I nod in agreement."

    "Because hentai.":
        "I grin."
        pf "Ohhhh, it all makes sense now."
        show kaori ske
        "Kaori raises an eyebrow."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_133.ogg"
        ki "Huh? What do you mean?"
        pf "Don't worry, even the eroge VN's can have good storylines and character development. I'm not judging you!"
        show kaori sur b2 with dissolve
        "Kaori's face flushes."
        show kaori ann b2
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_134.ogg"
        ki "That's not why I play them!"
        pf "...But you \"do\" play them?"
        show tsuBlush:
            xoffset 720
            yoffset 110
            xzoom .75
            yzoom .75
        show kaori thi b2
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_135.ogg"
        ki "Y-You're twisting my words! I play them for the story! I skip any of the {i}other{/i} stuff."
        pf "Suuuure."
        show kaori ann b1 with dissolve
        "Kaori punches my arm, although more gently than she usually does."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_136.ogg"
        ki "Stop being a pervert!"
        "I rub my arm."
        pf "Still hurts…"

show kaori smi with dissolve
stop music fadeout 4
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_137.ogg"
ki "Anyway, I think learning how to make a visual novel would be interesting."
pf "It would be. Let's go find good seats."
hide kaori with dissolve
"Kaori nods and we head inside. Before long, the room fills up and the panelists take their seats in the front of the room."

show receptionist extra at cc with dissolve
play music "audio/music/Rivals (GAME VERSION).ogg" fadein 4
voice "audio/voice/E4/Dishu/Kaori/1b.ogg"
dk "Hello everyone, I'm Dishu from PixelFade, and these are my colleagues."
"The team in turn introduces themselves as Katie, Sunimu, Rishu, and Chris."
voice "audio/voice/E4/Dishu/Kaori/2b.ogg"
dk "So, who's ready to make a visual novel?"
"The crowd cheers in affirmation. I look over at Kaori who watches attentively."
voice "audio/voice/E4/Dishu/Kaori/3b.ogg"
dk "Let's talk about how to make a school slice of life romance visual novel!"
voice "audio/voice/E4/Dishu/Kaori/4b.ogg"
dk "We have a four step approach to this."
voice "audio/voice/E4/Dishu/Kaori/5b.ogg"
dk "First, you should pick popular character tropes for the romance options and make them all into waifu material."
voice "audio/voice/E4/Dishu/Kaori/6b.ogg"
dk "I would highly recommend including at least one tsundere archetype since it seems to be a popular choice!"
voice "audio/voice/E4/Dishu/Kaori/7b.ogg"
dk "Secondly, make sure you have an abundance of comedy. Clever fourth wall breaking may be appropriate."
voice "audio/voice/E4/Dishu/Kaori/8b.ogg"
dk "Thirdly, you want to make sure the player is engaged. Give them choices so they feel immersed and active in the world."


menu:
    "This Dishu guy is right!":
        "I nod. He raises some good points."

    "I don't know about that...":
        "Ehh… choices that don't do anything feel hollow."

        
voice "audio/voice/E4/Dishu/Kaori/9b.ogg"
dk "And lastly, you must include words and phrases like {i}onii-chan{/i}, {i}sugoi desu ne{/i}, {i}senpai{/i}, and {i}baka{/i} at every possible opportunity."

voice "audio/voice/E4/Dishu/Kaori/10b.ogg"
dk "If people start saying your project is for weeaboos, then you're on the path to success!"


"The panel continues and each person elaborates on their specific role. They provide a lot of insider information and the crowd seems to be enjoying it. It's refreshing to see that although these guys take their craft seriously, they certainly don't take themselves seriously."
"Additional to this, they provide inside information about budgeting for indie development. {w}We learn that sometimes compromises have to be made due to limited resources, such as black silhouettes instead of full sprites for extra characters or even cameo characters."
"Dishu reassures us that as developers, you can trust your fanbase to understand!"

stop music fadeout 20
voice "audio/voice/E4/Dishu/Kaori/11b.ogg"
dk "That's it from us! Thank you all for coming out!"
hide receptionist extra with dissolve
"The crowd claps as they exit. Kaori and I also head out."
pf "What'd you think?"
show kaori smi at cc with dissolve
"She perks up."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_138.ogg"
ki "It was really good! They covered a broad range of subjects."
pf "Agreed. So what's next on the agenda?"
show kaori cur
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_139.ogg"
ki "Well, I'm looking forward to the panel with singer AmaLee! It's not for another hour though, so let's go explore the market."
pf "Sounds good to me."
scene black with fade

scene bg vacation comicon day with fade

"We head off to the marketplace and browse all the booths. There's merchandise ranging from mangas, wall scrolls, cosplays, mock weapons, artwork, and tons more."

show kaori sur b1 at cc with dissolve

voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_140.ogg"
ki "Ah!"
"Kaori squeals and darts to one of the stalls."
show kaori smi
play music "audio/music/Day Out (GAME VERSION).ogg" fadein 4
"She picks up a stuffed yellow thingy with red cheeks and squeezes it close to her cheek."
show kaori hap
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_141.ogg"
ki "Peekatchoo! It's so cute!"
"I stare at her."
if E2D5KI_Completed == 1:
    "The last time I saw her like this was after a certain victory party and too many shots…"
show kaori cur b1
"Kaori notices my stare, then blushes and puts down the stuffed Peekatchoo."
show kaori thi b1
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_142.ogg"
ki "I-I mean, if you're into that sort of thing."

menu:
    "Get it for her as a gift!":
        $ E4KIS3_BoughtGift = 1
        "I pick up Peekatchoo again and balance it atop her head so it looks like it's giving her a hug. I look straight into her eyes."
        show kaori cur b1
        pf "It's adorable."
        "She blushes. I turn to the vendor."
        pf "How much?"
        
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_143.ogg"
        ki "Wait, I can pay for it--"
        pf "No."
        show kaori ske b1
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_144.ogg"
        ki "Really you don't--"
        pf "No!"
        "I quickly tap my card against the reader before Kaori can pull out her own card."
        show kaori ann b1
        "She pouts and looks like she wants to argue again. I put the Peekatchoo in front of my face and pretend it's talking."
        pf "Don't make that face at [pfirst]! He's just being nice."
        show kaori cur b1
        "Then I use the Peekatchoo to try and hug her face. She dodges but I am relentless! I can hear her giggle."
        show kaori hap b2 with dissolve
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_145.ogg"
        ki "S-Stop!"
        show kaori smi b2
        "I grin and hand the gift to her. She smiles and squeezes it against her chest."
        show regBlush:
            xoffset 720
            yoffset 110
            xzoom .75
            yzoom .75
        ki "Thank you."

    "I'll be bankrupt if I buy everything she ends up liking.":
        pf "Are you getting it?"
        show kaori neu
        "Kaori thinks for a moment, then shakes her head."
        voice "audio/voice/MISSING/BATCH5/k_redo_19.ogg"
        ki "Thank you."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_147.ogg"
        ki "I'd like to see what else is around first."
        pf "Alright."

    "We can always get it later.":
        pf "Let's take a look and see what else is around. We can always get it later."
        show kaori neu
        "Kaori thinks then nods."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_148.ogg"
        ki "Good idea."

hide kaori with dissolve
"We wander around the market place for a while longer and spend equal time checking out people's cosplays as well as the booths. We found PixelFade's stall and made a point to look at their merch. They had some really cool art and posters featuring something called \"Crystalline\"... whatever that is."

show kaori cur at cc with dissolve
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_149.ogg"
ki "It's almost time for the AmaLee panel. Let's go!"

if E4KIS3_BoughtGift == 0:
    pf "What about Peekatchoo?"
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_150.ogg"
    ki "Oh yeah!"
    "We head back to that vendor, but there is no yellow ball of fluff around."
    voice "audio/voice/E4/Vendor/E4/Kaori/1.ogg"
    vg "Sorry, it sold right after you guys left."
    show kaori sad
    "Kaori looks disappointed. I pat her gently on the back."
    pf "It's okay, I'm sure we'll find some other nice souvenirs."
    show kaori thi
    "She tries to brush it off."
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_151.ogg"
    ki "It doesn't matter anyway. It's not like I needed it."   
    show kaori smi
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_152.ogg"
    ki "Let's hurry up and get to the panel so we can find good seats!"

pf "Lead the way!"

scene black with fade

"The AmaLee panel is already mostly full by the time we show up, but we still manage to find decent seats."
"A few minutes later, the crowd erupts into cheers and applause as AmaLee walks out. She smiles and waves as she takes her seat at the solo panel."



scene cg amanda lee 2 with fade:
    xzoom .5
    yzoom .5

voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line1.ogg"
    
ama "Hello everyone!"

scene cg amanda lee 1 with dissolve:
    xzoom .5
    yzoom .5
    
"The panel begins and she talks about what she has planned, including which songs will be getting covered, and how songs are created."
"Judging by the rapt attention of the audience, everyone seems to be enjoying the discussion."
"Soon, it comes time for the Q&A section of her panel. A line forms in front of a mic to ask her questions."



voice "audio/voice/E4/amaguest/guestlinee1.ogg"
GuestM "What’s your favorite cover to have worked on?"

scene cg amanda lee 4 with dissolve:
    xzoom .5
    yzoom .5
    
voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line2.ogg"
    
ama "The ones that are the most challenging are usually my favourite. It's fun to try something new!"

"The guy grins as the next person steps up to the microphone."

voice "audio/voice/MISSING/BATCH1/7.ogg"
GuestF "Do you have any singing tips?" 

scene cg amanda lee 1 with dissolve:
    xzoom .5
    yzoom .5
    
"AmaLee nods and looks determined."
voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line3.ogg"

ama "Practice and practice. And then when you think you’ve done enough, practice some more! It’s a simple and boring answer, but it’s the truth!"

"Because we only see the end result, it's easy to forget that each video includes hours of practice and work. {w}The audience mumbles in agreement as another person reaches for the mic."

voice "audio/voice/E4/amaguest/guestlinee2.ogg"
GuestM "Why are your lyrics different from the source translations?!"

"Uh oh, it's \"that\" guy from the MeTube comments... {w}The rest of the audience glares at him but AmaLee smiles politely."

scene cg amanda lee 4 with dissolve:
    xzoom .5
    yzoom .5
voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line4.ogg"
    
ama "Well, direct translations never fit the meter of the song, so they have to be altered slightly to fit, and rhyme and flow."

"The guy doesn't seem content with the answer and rudely hogs the mic. People grumble in irritation, but he ignores them."

voice "audio/voice/E4/amaguest/guestlinee3.ogg"
GuestM "Yeah, but I took that language class in high school and used Googel translate... and it was all wrong!"

    
"AmaLee continues to smile, although I can tell it's now a little strained."

scene cg amanda lee 3 with dissolve:
    xzoom .5
    yzoom .5

voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line5.ogg"

ama "It's hard if you've never tried it yourself... But, if you take a direct translation and try to sing it to the song, you'll hear how awkward it sounds."

"The guy looks like he's about to ask another question, but one of the security guards reminds everyone of the one question rule. Reluctantly, he yields the mic."

scene cg amanda lee 1 with dissolve:
    xzoom .5
    yzoom .5
    
"The next person takes the microphone and smiles broadly."

voice "audio/voice/MISSING/BATCH1/6.ogg"
GuestF "Do you sing in the shower?"

"Any remaining tension dissipates as AmaLee chuckles."

scene cg amanda lee 4 with dissolve:
    xzoom .5
    yzoom .5
    
voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line6.ogg"

ama "Yeah, and just as badly as everyone else!"
"We hear a yell from behind us."
voice "audio/voice/E4/amaguest/guestlinee4.ogg"
GuestM "Lies!"
scene cg amanda lee 2 with dissolve:
    xzoom .5
    yzoom .5
"Both AmaLee and the crowd laugh."

scene black with fade

"The rest of the Q&A session continues without incident."


"There's a collective \"awww\" as AmaLee concludes her panel, and everyone rushes to get her autograph. The line is so long it stretches out into the hall!" 

scene bg vacation comicon day with fade

pf "Do you want to get a signature?"

show kaori cur at cc with dissolve
voice "audio/voice/E4/Kaori/Kaori AmaLee/Kaori_Amalee_01.ogg"
ki "...If it's not too much trouble I think I'd like that..."
pf "Sure thing."
scene black with fade
"We wait patiently in line. Eventually, it's Kaori's turn."
scene cg amanda lee 2 with fade:
    xzoom .5
    yzoom .5
    
voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line7.ogg"
    
ama "Hey!"
"Kaori blushes and stumbles to get her words out."
voice "audio/voice/E4/Kaori/Kaori AmaLee/Kaori_Amalee_02.ogg"
ki "H-Hi."
voice "audio/voice/E4/Kaori/Kaori AmaLee/Kaori_Amalee_03.ogg"
ki "C-Could I get your autograph?"
scene cg amanda lee 4 with dissolve:
    xzoom .5
    yzoom .5
    
voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line8.ogg"
    
ama "Of course! Who should I make it out to?"
voice "audio/voice/E4/Kaori/Kaori AmaLee/Kaori_Amalee_04.ogg"
ki "Kaori, please."
"AmaLee nods and scribbles onto a poster."
scene cg amanda lee 2 with dissolve:
    xzoom .5
    yzoom .5
    
voice "audio/voice/E4/AmaLee/AmaLee_EP4_Line9.ogg"
    
ama "Alright, here you go!"
"Kaori grabs the poster and smiles. We take a quick photo with AmaLee and walk out to the hall. The line doesn't seem to have shortened any."

scene black with fade
scene bg vacation comicon day with fade
show kaori smi b1 at cc with dissolve
"Kaori has a dreamy look in her eye."
show shiny:
    xoffset 720
    yoffset 110
    xzoom .75
    yzoom .75
voice "audio/voice/E4/Kaori/Kaori AmaLee/Kaori_Amalee_05.ogg"
ki "I can't believe I got to meet AmaLee!"
"I nod."
pf "Yeah, although I found it kind of strange..."
show kaori cur b1
voice "audio/voice/E4/Kaori/Kaori AmaLee/Kaori_Amalee_06.ogg"
ki "Hmm?"
pf "Doesn't she sound {i}exactly{/i} like Mei?"
show kaori sur b1
"Kaori's eyes widen."
voice "audio/voice/E4/Kaori/Kaori AmaLee/Kaori_Amalee_07.ogg"
ki "You think so too?! I was thinking the exact same thing but didn't want to say anything in case it was all in my head!"
show kaori thi b1
pf "A weird coincidence I suppose."
"We both nod."
show kaori smi b1
stop music fadeout 7
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_153.ogg"
ki "Okay, next on the agenda…"

"We spend a few more hours at the con alternating between listening to more panels and checking out the marketplace."

scene black with fade

"As we run through Kaori's list and things begin to wind down, Kaori checks her phone."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_154.ogg"
ki "It's getting pretty late… We should check into our hotel."
pf "Sure."
"We'd managed to snag a room a few nights before at the hotel right around the corner from the event. I thought they would have been sold out but we got lucky!"
play music "audio/music/Hanging Out (GAME VERSION).ogg" fadein 5
scene bg campus office day with fade

"We approach the front desk and Kaori provides our name. The front desk agent types into her computer, then frowns."
show professorF extra at r2:
    xzoom -1
show kaori neu at l2
with dissolve
voice "audio/voice/E4/Kaori_Receptionist/Receptionist_01.ogg"
recep "I'm sorry, I can't seem to find your reservation…"
voice "audio/voice/E4/Kaori_Receptionist/Receptionist_02.ogg"
recep "Do you have the confirmation number?"
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_155.ogg"
ki "Um, yeah, hold on."
"She rummages through her emails and recites a code."
"The front desk agent checks again and shakes her head."
voice "audio/voice/E4/Kaori_Receptionist/Receptionist_03.ogg"
recep "I'm very sorry..."
"Kaori shoves her phone in front of the agent and shows her the email."
show kaori thi
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_156.ogg"
ki "I don't understand. I booked it a few days ago! Here's the confirmation I got."
"The agent reviews it."
voice "audio/voice/E4/Kaori_Receptionist/Receptionist_04.ogg"
recep "Unfortunately, since you booked through a third party agency there must have been a complication and we never received the reservation. We're at full capacity for the room type that you booked…"
show kaori ner
"Kaori looks worried."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_157.ogg"
ki "Isn't there anything you can do?"
"I was expecting her to blow up but she is surprisingly calm. Kaori notices me staring at her."
show kaori cur
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_158.ogg"
ki "Um?"
pf "Oh, uh, nothing, sorry."
show kaori neu
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_159.ogg"
ki "Okay..."
voice "audio/voice/E4/Kaori_Receptionist/Receptionist_05.ogg"
recep "Let me see…"
voice "audio/voice/E4/Kaori_Receptionist/Receptionist_06.ogg"
recep "We do have a suite available. Since this was a system glitch, I'll waive the upgrade cost and will charge you the same price you were quoted for the standard room." 
pf "Thanks! We really appreciate that."
"Kaori blinks."
show kaori smi
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_160.ogg"
ki "Thank you."
voice "audio/voice/E4/Kaori_Receptionist/Receptionist_07.ogg"
recep "No problem! Again, sorry for the inconvenience."
"She hands us our keys and goes through the usual spiel about the rooms."

voice "audio/voice/E4/Kaori_Receptionist/Receptionist_08.ogg"
recep "We hope you enjoy your stay!"

scene black with fade

"Kaori and I head into the elevator and find our room. We insert the key, and as we walk in, my jaw drops."

scene bg vacation hotel night with fade

"The suite is beautifully furnished with a king bed and a chaise lounge. A two person dining room table and chairs is by the wide window which overlooks the city. Soft candlelight castes playful shadows on the rose petals scattered on the bed, and a box of chocolates is enticingly opened on the pillows."
"A \"suite\", huh… more like a \"honeymoon suite\"!"
"My eyes drift back to the {i}one{/i} bed in the room…"

menu:
    "I don't know if I'm ready, Kaori...":
        "My cheeks flush."
        pf "This is all moving so fast."
        show kaori sur b2 at cc with dissolve
        show shoBlush:
            xoffset 720
            yoffset 110
            xzoom .75
            yzoom .75
        "Kaori stammers out a reply, her face equally red."

        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_161.ogg"
        ki "I-It's not what you think! I originally booked two beds!"

    "I LIKE WHERE THIS IS GOING.":
        "I smile appreciatively at the display."
        pf "I didn't know you had this side to you, Kaori."
        show kaori ann b1 at cc with dissolve
        "Her eyes widen and she punches me hard on the arm."
        show vein:
            xoffset 720
            yoffset 110
            xzoom .75
            yzoom .75
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_162.ogg"
        ki "Stop being a pervert! I didn't book this! Ugh."

    "This is definitely a misunderstanding.":
        pf "This doesn't seem right."
        show kaori ann b1 at cc with dissolve
        "Kaori's cheeks are stained red as she nods."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_163.ogg"
        ki "This {i}isn't{/i} right."

"She stomps to the phone and dials the front desk."

stop music fadeout 10
show kaori ann with dissolve
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_164.ogg"
ki "What is this room?!"
show kaori ang
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_165.ogg"
ki "The room I booked had two beds. There's only one in here and other... {i}weird{/i} things."
show kaori thi
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_166.ogg"
ki "What do you mean there are no more rooms with two beds? That's what--"
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_167.ogg"
ki "Yes, I can see there's a couch."
"Kaori grumbles then hangs up the phone."
pf "Soooo, that sounds like it went well…"
show kaori neu
"Kaori sighs."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_168.ogg"
ki "We're stuck with the room. Nothing else is available and I doubt any other hotels will have rooms either due to the convention."
pf "The last train to Isokaze departed an hour ago…"
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_169.ogg"
ki "Yeah."
"I look between the lounge and the very comfortable looking bed."

label E4KIS3_Loopback:
menu:
    "I can sleep on the lounge.":
        play music "audio/music/After a Long Day (GAME VERSION).ogg" fadein 3
        pf "It's fine, I can sleep on the lounge."
        show kaori cur
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_170.ogg"
        ki "Are you sure?"
        pf "Yeah. I don't mind. I only came today to spend time with you."
        show kaori neu b1 with dissolve
        "She blushes."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_171.ogg"
        ki "T-Thank you."
        "I hop onto the lounge and settle in. It's not too bad. I've definitely slept on worse."
        show kaori wor b1
        "Kaori sits atop the bed, then looks at me."
        pf "Seriously, don't worry about it."
        show kaori sad b1
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_172.ogg"
        ki "Okay..."
        hide kaori with dissolve
        "We both get ready for bed. I return to my couch and roll over, trying to get comfortable, then close my eyes."
        scene black with fade
        $renpy.pause(.75)
        scene bg vacation hotel night with fade
        "After a few minutes, I hear the bathroom door open. A soft pressure dips the couch and I open my eyes to see Kaori sitting beside me. I immediately scoot up into a sitting position."
        pf "Kaori…?"
        show kaori smi b2 at cc with dissolve
        "She snuggles into me and wraps her arms around my neck."
        "My heart beats a mile a minute."
        pf "K-Kaori…!"
        show kaori thi b3 with dissolve
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_173.ogg"
        ki "Thanks… for everything."
        "Hesitantly, she leans in and kisses me gently on the lips."
        show heart:
            xoffset 720
            yoffset 110
            xzoom .75
            yzoom .75
        show kaori smi b3
        "She lingers with me on the lounge and we silently enjoy each other's company. As my eyes start to droop, I'm barely aware of Kaori planting one last kiss before slipping away, and I fall asleep." 
        hide kaori with dissolve
        scene black with fade
        jump E4KIS3_conv


    "...We can share?" if E4KIS3_Shareoffer == 0:
        $ E4KIS3_Shareoffer = 1
        pf "I mean, the bed is pretty big… it can easily fit two people..."
        show kaori ner b1 with dissolve
        "Kaori blushes, and shakes her head."
        "Well then."
        jump E4KIS3_Loopback

    "FINE, I'LL TAKE THE BED THEN!" if E4KIS3_Shareoffer == 1:
        $ E4KIS3_SleepTogether = 1
        play music "audio/music/Baka! (GAME VERSION).ogg" fadein 3
        pf "Alright, then I'll take the bed."
        "I dive onto the bed and starfish in the middle."
        show kaori sur
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_174.ogg"
        ki "What?!"
        pf "What do you mean \"what\"? I offered to share and you said no, so I'm taking the bed."
        show kaori ann
        "Kaori scowls and clenches her hands into fists."
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_175.ogg"
        ki "Get off of there!"
        pf "Make me!"
        show kaori dis
        "She grabs ahold of my arms and tries to pull me off. She's barely able to move me. Then she crosses to the other side of the bed and attempts to roll me off, but I plant myself firmly in place and she's equally unsuccessful."
        show kaori sad
        "Kaori pouts and pleads with me with wide eyes."
        "Ah, the puppy dog eyes. Kaori is amateur hour compared to Nikki. Who would have known having a little sister would provide such useful training!"
        pf "Nice try."
        show kaori ann
        stop music fadeout 12
        voice "audio/voice/MISSING/BATCH5/k_redo_20.ogg"
        ki "You are such a jerk!"
        hide kaori with dissolve
        "Kaori grabs the extra pillows on the bed and the lounge and creates a barrier right down the middle of the bed."
        "She sits down on her side as close to the far end of the bed as possible."
        show kaori ann at cc with dissolve
        
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_177.ogg"
        ki "You stay on your side and I'll stay on mine."
        
        pf "That's what I was saying from the start!"
        show kaori dis
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_178.ogg"
        ki "No funny business!"
        pf "No funny business."
        show kaori neu
        voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_179.ogg"
        ki "Fine."
        hide kaori with dissolve
        $renpy.pause(.5)
        scene black with fade
        "We both get ready for bed. I hadn't realised just how exhausted I was until my head hit the pillow. The bed is like a soft cloud cradling me."
        "I can occasionally feel movement, but I'm too tired for it to be a real distraction."
        "It takes no time at all before I drift to sleep..."
        $renpy.pause(2.5)
        "..."
        "!"
        "My eyes fly open in the darkness. I'm not sure what woke me, but I feel a small hand against my chest."
        
        play music "audio/music/Tender Moments (GAME VERSION).ogg" fadein 10
        $ persistent.gpix[36][0] = 1
        scene cg kaori hotel 1 with Dissolve(2.5):
            xzoom .5
            yzoom .5
        
        "As my eyes adjust to the darkness, I look down to see Kaori's sleeping face. Her brows and mouth are relaxed; she looks so peaceful yet vulnerable without her fierce scowl."
        "I gently wrap my arms around her and hold her against me."
        "She stirs a bit but doesn't wake up. Instead, she snuggles even closer to me. I nuzzle my face in her hair and drink in her scent."
        "Her body fits perfectly in my arms, as if we are two pieces of a puzzle, and the gentle up and down of her breathing relaxes me."
        "I wish this moment will never end."
        
        $ qtetotal = 5
        $ t_var = qtetotal
        show screen timer_scr(place="E4KIS3_nolove")
        
        menu:
            "I love you.":
                $ renpy.hide_screen ("timer_scr")
                $ E4KIS3_LoveYou = 1
                "I want to have more moments like this. I want to hold her in my arms in the tranquility of the night and sleep to the rhythm of her heartbeat."
                "She's the fire that fuels my passion, and now that I have her, I can't imagine a life without her."
                "Of one thing I am certain."
                stop music fadeout 4
                pf "I love you."
                jump E4KIS3_conv

            
        label E4KIS3_nolove:
        $ renpy.hide_screen ("timer_scr")
        stop music fadeout 4
        "Listening to the rhythm of her heartbeat, I gradually fall asleep with Kaori in my arms."
        jump E4KIS3_conv



label E4KIS3_conv:
scene black with Dissolve(1.5)
$renpy.pause(2.5)

play ambient "audio/ambience/Morning.ogg" fadein 3
$renpy.pause(1)

"The sharp ringing of cutlery wakes me up."

scene bg vacation hotel day with fade

if E4KIS3_SleepTogether == 1:
    "I glance at the empty space beside me and panic before seeing Kaori sitting by the table."
else:
    "Kaori sits by the table."
    
play music "audio/music/Isokaze (GAME VERSION).ogg" fadein 5
show kaori cur at cc with dissolve
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_180.ogg"
ki "Oh, did I wake you? Sorry."
"I push myself up to a sitting position."
pf "Good morning."
show kaori smi
"Kaori smiles at me."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_181.ogg"
ki "Morning."
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_182.ogg"
ki "I ordered room service. I hope that's okay."
"My stomach growls as the delicious smells of breakfast wafts in the air. It's been so long since I've had a traditional Japanese breakfast."
pf "This is perfect."
"I join Kaori at the table and we dig in. I can't keep the smile off my face as we eat. Is this what it's like to be a couple?{w} Kaori casually meets my eyes, then blushes."

if E4KIS3_LoveYou == 1:
    pf "Did you sleep okay?"
    show kaori cur b2 with dissolve
    "Kaori blushes even more. I wonder how she felt waking up entwined in my arms."
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_183.ogg"
    ki "...Yes."
    show kaori thi b2
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_184.ogg"
    ki "Actually, I had a really weird dream..."
    pf "Oh?"
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_185.ogg"
    ki "Yeah. It was you and me and we were at a restaurant."
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_186.ogg"
    ki "Or I think it was a cafe."
    pf "That's not strange considering we've done that before."
    show kaori neu b2
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_187.ogg"
    ki "I know. We were just chatting like we always do, but then, out of nowhere, you said..."
    show kaori cur b3 with dissolve
    "Her face flushes and she shakes her head."
    show kaori win b3 with dissolve
    show drop:
        xoffset 720
        yoffset 110
        xzoom .75
        yzoom .75
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_188.ogg"
    ki "N-Nevermind! Forget I said anything!"
    show kaori thi b3
    voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_189.ogg"
    ki "Hurry up and eat your breakfast or we'll miss our train!"
    "I grin. I guess she did hear me afterall."
    "Kaori keeps shooting glances at me as we finish eating. The more she looks at me, the more embarrassed she becomes."

"Once done with breakfast, we pack up our things and catch the train back to Isokaze."

stop ambient fadeout 2
scene black with fade

"Kaori stares out the window like before. I try to focus on the scenery too, but my gaze keeps slipping back to her."

scene bg travel trainstation dusk with fade

"When we finally arrive at Isokaze station, we face each other on the platform."
show kaori smi b1 at cc with dissolve
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_190.ogg"
ki "Thanks for coming with me. I had a really great time."
pf "Me too."
show kaori smi b2 with dissolve
"She hesitates. Then quickly hugs me and lets me go."
show heart:
    xoffset 720
    yoffset 110
    xzoom .75
    yzoom .75
voice "audio/voice/E4/Kaori/Kaori Arc/Kaori_event_191.ogg"
ki "I-I'll see you again on Monday!"
pf "If not sooner."
hide kaori with dissolve

"She smiles, then leaves to catch her bus back to ACE while I drive back home."
scene black with fade
stop music fadeout 3
stop ambient fadeout 3
$renpy.pause(2.5)
$ E4KIS3_Completed = 1
jump E4D7S1